/**
 * Provides docker container settings
 */

plugins {
    id 'base'
    id 'com.palantir.docker'
    id 'com.palantir.docker-run'
    id 'com.palantir.docker-compose'
    id 'starter.java.property-conventions'
}

ext {
    dockerRegistry = project.hasProperty("dockerRegistry") ? "${project.dockerRegistry}" : "${group}"
    dockerImageVersion = project.hasProperty("buildNumber") ? "${project.version}-${project.buildNumber}" : project.version
}

docker {
    dependsOn(assemble)
    name "${dockerRegistry}/${rootProject.name}"
    tag "Build", "${dockerRegistry}/${rootProject.name}:${dockerImageVersion}"
    tag "Latest", "${dockerRegistry}/${rootProject.name}:latest"
    noCache true
    dockerfile file('src/docker/Dockerfile')
}

dockerRun {
    name project.name
    image "${dockerRegistry}/${rootProject.name}"
    ports '8080:8080'
    env 'SECRETHUB_HELLO': getEnvOrDefault('SECRETHUB_HELLO', 'override-me')
}

dockerCompose {
    dockerComposeFile 'src/docker/docker-compose.yml'
}

def dockerStart = tasks.register('dockerStart', GradleBuild) {
    tasks = ["dockerPrune", "clean", "dockerClean", "docker", "dockerRun"]
}

def dockerPrune = tasks.register('dockerPrune', Exec) {
    mustRunAfter('dockerStop', 'dockerRemoveContainer')
    executable "${project.rootDir}/scripts/docker-prune.sh"
    args "--container", "--image"
}

def dockerVolumePrune = tasks.register('dockerVolumePrune', Exec) {
    mustRunAfter('dockerStop', 'dockerRemoveContainer', 'dockerPrune')
    executable "${project.rootDir}/scripts/docker-prune.sh"
    args "--volume"
}

def dcPrune = tasks.register('dcPrune', Exec) {
    mustRunAfter('dockerComposeDown')
    executable "${project.rootDir}/scripts/docker-prune.sh"
    args "--container", "--image"
}

def dcVolumePrune = tasks.register('dcVolumePrune', Exec) {
    mustRunAfter('dockerComposeDown', 'dcPrune')
    executable "${project.rootDir}/scripts/docker-prune.sh"
    args "--volume"
}

dockerRemoveContainer.configure {
    mustRunAfter('dockerStop')
}

/*
task dockerSterilize(type: GradleBuild) {
    tasks = ["dockerComposeDown",
             "dockerStop",
             "dockerRemoveContainer",
             "clean",
             "dockerClean",
             "dockerPrune",
             "dockerVolumePrune"]
}

 */

