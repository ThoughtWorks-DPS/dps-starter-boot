/**
 * Configurations for testing
 */

plugins {
    id 'starter.java.property-conventions'
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output
            //compileClasspath += test.output
            runtimeClasspath += main.output
            //runtimeClasspath += test.output
            srcDir file('src/integration/java')
        }
        resources.srcDir file('src/integration/resources')
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

def integrationTest = tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter tasks.named('test')
    useJUnitPlatform {
        excludeEngines 'junit-vintage'
    }
}

check.configure {
    dependsOn integrationTest
}


